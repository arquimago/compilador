Package Cmenos;

Helpers

  numero = ['0'..'9'];
  letra = ['a'..'z']|['A'..'Z'];
  igual = '=';
  asterisco = '*';
  barra = '/';
  ponto_virgula = ';';
  virgula = ',';
  parentese_e = '(';
  parentese_d = ')';
  colchete_e = '[';
  colchete_d = ']';
  chave_e = '{';
  chave_d = '}';
  aspas ='"';

  mais = '+';
  menos = '-';
  multiplicacao = asterisco;
  divisao = barra;
  menor = '<';
  maior = '>';

  menor_igual = menor igual;
  maior_igual = maior igual;
  igualdade = igual igual;
  diferente = '!' igual;

  comentario_e = barra asterisco;
  comentario_d = asterisco barra;
  comentario_l = barra barra;

  nova_linha = 10;
  enter = 13;
  tab = 9;
  espaco = 32;
  quebra = enter|nova_linha|enter nova_linha|;
  branco = espaco|quebra|tab;

  caracter = [0..255];

  texto_linha = [caracter-[10+13]]*;
  texto_bloco = [caracter-[asterisco+barra]]*;
  comentario_linha = comentario_l texto_linha quebra;
  str = aspas (caracter)* aspas;

  reservadas = 'else'|'if'|'int'|'string'|'return'|'void'|'while';


States

  normal,comentario;


Tokens

  {normal, comentario} comentario_linha = comentario_linha;

  {normal -> comentario, comentario}  comentario_e = comentario_e;
  {comentario} comentario_corpo = texto_bloco;
  {normal -> comentario, comentario} comentario_d = comentario_d;
  {comentario} asterisco = asterisco;
  {comentario} barra = barra;

  {normal} if = 'if';
  {normal} else = 'else';
  {normal} int = 'int';
  {normal} string = 'string';
  {normal} return = 'return';
  {normal} void = 'void';
  {normal} while = 'while';
  {normal} input = 'input';
  {normal} output = 'output';

  {normal} id = letra letra*;
  {normal} num = numero numero*;
  {normal} str = str;
  {normal} branco = branco;

  {normal} soma = mais|menos;
  {normal} mult = divisao|multiplicacao;

  {normal} comparador = menor|menor_igual|maior|maior_igual|igualdade|diferente;
  {normal} atribuicao = igual;
  {normal} ponto_virgula = ponto_virgula;
  {normal} virgula = ',';
  {normal} parentese_e = parentese_e;
  {normal} parentese_d = parentese_d;
  {normal} colchete_e = colchete_e;
  {normal} colchete_d = colchete_d;
  {normal} chave_e = chave_e;
  {normal} chave_d = chave_d;
  {normal} idfunc = letra letra* parentese_e;


Ignored Tokens

  comentario_linha, branco, comentario_e, comentario_corpo, comentario_d;


Productions

  programa =
    {lista} lista_declaracoes;

  lista_declaracoes =
    {lista} lista_declaracoes declaracao |
    {declaracao} declaracao;

  declaracao =
    {variavel} variavel_dec |
    {funcao} funcao_dec;

  variavel_dec =
    {nula} tipo id ponto_virgula |
    {matriz} tipo id colchete_e num colchete_d ponto_virgula;

  tipo =
    {t_void} void |
    {t_int} int |
    {t_str} string;

  funcao_dec =
    tipo id parentese_e parametros parentese_d composto_dec;

  parametros =
    {lista} lista_parametros |
    {vazio} void;

  lista_parametros =
    {parametros} lista_parametros virgula parametro |
    {parametro} parametro;

  parametro =
    {simples} tipo id |
    {matriz} tipo id colchete_e colchete_d;

  composto_dec =
    {declaracoes_locais} chave_e declaracoes_locais lista_comandos chave_d;

  declaracoes_locais =
    {lista} declaracoes_locais variavel_dec |
    {vazio} ;

  lista_comandos =
    {lista} lista_comandos comando |
    {vazio} ;

  comando =
    {expressao} espressao_dec |
    {comandos} composto_dec |
    {if} if parentese_e expressao parentese_d composto_dec |
    {if_then_else} if parentese_e expressao parentese_d [v]:composto_dec
    else [f]:composto_dec |
    {if_else} if parentese_e expressao parentese_d ponto_virgula else composto_dec|
    {bloco_while} bloco_while |
    {retorno} retorno;

  espressao_dec =
    {espressao} expressao ponto_virgula |
    {vazio} ponto_virgula;

  bloco_while =
    while parentese_e expressao parentese_d comando;

  retorno =
    {vazio} return ponto_virgula |
    {com_expressao} return expressao ponto_virgula;

  expressao =
    {atribuicao} var atribuicao expressao |
    {atribuicao_str} var atribuicao str |
    {simples} expressao_simples;

  var =
    {id_simples} id |
    {id_array} id array;

  array =
    {array_multiplo} array colchete_e expressao colchete_d |
    {array_base} colchete_e expressao colchete_d;

  expressao_simples =
    {comparativo} [exp1]:soma_expressao comparador [exp2]:soma_expressao |
    {aditiva} soma_expressao;

  soma_expressao =
    {soma} soma_expressao soma termo |
    {termo} termo;

  termo =
    {mult} termo mult fator |
    {fator} fator;

  fator =
    {espressao} parentese_e expressao parentese_d |
    {variavel} var |
    {chamada} chamada |
    {numero} num;

  chamada =
    idfunc args parentese_d;

  args =
    {lista} lista_args |
    {vazio} ;

  lista_args =
    {lista} lista_args expressao |
    {expressao} expressao;
